State {
  curtab : chan;
  output : chan;
}

Components {
  UserInput "/home/ucsd/vcr/python-browser-8/input.py";
  Output    "/home/ucsd/vcr/python-browser-8/output.py";
  Tab       "/home/ucsd/vcr/python-browser-8/tab.py" {
    domain : str;
  }
  DomainBar "/home/ucsd/vcr/python-browser-8/domainbar.py";
}

Messages {
  Display     (str);
  Navigate    (str);
  ReqResource (str);
  ResResource (fdesc);
  ReqSocket   (str);
  ResSocket   (fdesc);
  SetDomain   (str);
# Input
  KeyPress   (str);
  MouseClick (str);
# Specific to Kraken implementation
  Go (str);
}

Init {
  output = spawn(Output);
  curtab = spawn(Tab("google.com"));
  send(curtab, Go("http://www.google.com"));
  spawn(UserInput);
}

Exchange(c) {
  Tab {
    Display(s) when (c =c curtab) {
      send(output, Display(s));
    } {
      # Drop display requests from non-current tabs
    }

    Navigate(url) when (dom(url) =s c@domain) {
      # This should not happen, don't do anything
    } {
      curtab = spawn(Tab(dom(url)));
      send(curtab, Go(url));
    }

    ReqResource(url) {
      res = call("wget.py", url);
      send(c, ResResource(res));
    }

    ReqSocket(url) when (dom(url) =s c@domain) {
      soc = connect(url);
      send(c, ResSocket(soc));
    } {
      # Do not open sockets to other domains
    }
  }

  UserInput {
    KeyPress(key) {
      send(curtab, KeyPress(key));
    }

    MouseClick(pos) {
      send(curtab, MouseClick(pos));
    }
  }
}
