Components :
  Engine     "engine.c"     () ()
  Brakes     "brakes.c"     () ()
  CruiseCtrl "cruisectrl.c" () ()
  Doors      "doors.c"      () ()
  Radio      "radio.c"      () ()
  Airbag     "airbag.c"     () ()
  Alarm      "alarm.c"      () ()

Messages :
  Crash()
  Acceleration()
  DoorsOpen()
  UnlockDoors()
  LockDoors()
  VolumeUp()
  VolumeDown()
  InflateAirbag()
  BrakesApplied()
  CruiseOff()

State :
  crashed : num

Operations:

Init :
  crashed <- 0;
  E <- spawn Engine();
  B <- spawn Brakes();
  CC <- spawn CruiseCtrl();
  D <- spawn Doors();
  R <- spawn Radio();
  AB <- spawn Airbag();
  Alrm <- spawn Alarm()

Handlers :
  When e:Engine sends Crash() respond:
    crashed <- 1;
    lookup Airbag()
    { AB => lookup Doors()
            { D => send(AB, InflateAirbag());
                   send(D, UnlockDoors()) }
            { nop } }
    { nop }

  When e:Engine sends Acceleration() respond:
    lookup Radio()
    { R => send(R, VolumeUp()) }
    { nop }

  When d:Doors sends DoorsOpen() respond:
    lookup Radio()
    { R => send(R, VolumeDown()) }
    { nop }

  When a:Alarm sends LockDoors() respond:
    if (crashed == 0)
    then lookup Doors()
         { D => send(D, LockDoors()) }
         { nop }
    else nop

  When b:Brakes sends BrakesApplied() respond:
    lookup CruiseCtrl()
    { CC => send(CC, CruiseOff()) }
    { nop }

Properties :
  AirbagImmAfterCrash:
    [Send(Airbag(), InflateAirbag())]
    ImmAfter
    [Send(Engine(), Crash())]

  CarNonInterference:
    NoInterfere
      HighComps : Engine()
      HighVars :
      HighCompList : Engine(), Brakes(), CruiseCtrl(),
                     Doors(), Radio(), Airbag(), Alarm()

  CrashDisablesLock:
    [Recv(Engine(), Crash())]
    Disables
    [Send(_, LockDoors())]

  CrashEnablesAirbag:
    [Recv(Engine(), Crash())]
    Enables
    [Send(Airbag(), InflateAirbag())]

  CrashEnsuresAirbag:
    [Recv(Engine(), Crash())]
    Ensures
    [Send(Airbag(), InflateAirbag())]

  CrashEnsuresUnlock:
    [Recv(Engine(), Crash())]
    Ensures
    [Send(Doors(), UnlockDoors())]

  CruiseOffImmAfterBrakes:
    [Send(CruiseCtrl(), CruiseOff())]
    ImmAfter
    [Recv(Brakes(), BrakesApplied())]

  UnlockImmAfterAirbag:
    [Send(Doors(), UnlockDoors())]
    ImmAfter
    [Send(Airbag(), InflateAirbag())]